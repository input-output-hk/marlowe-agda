module Marlowe.Semantics.Reduce where

open import Data.Bool using (Bool; if_then_else_; not; _âˆ§_; _âˆ¨_; true; false)
open import Data.Bool.Properties using (_â‰Ÿ_; Â¬-not)
open import Data.Integer as â„¤ using (â„¤; 0â„¤; _â‰¤_; _>_; âˆ£_âˆ£; _<?_; _â‰¤?_)
open import Data.Integer.Properties as â„¤ using ()
open import Data.List using (List; []; _âˆ·_; _++_; foldr; reverse; [_]; null; sum; filter; map)
open import Data.List.Membership.Propositional using () renaming (_âˆˆ_ to _âˆˆ-List_)
open import Data.List.Relation.Unary.Any using (Any; lookup; _â”€_; _âˆ·=_; here; there)
open import Data.List.Relation.Unary.All.Properties using (Â¬Anyâ‡’AllÂ¬; AllÂ¬â‡’Â¬Any)
open import Data.Maybe using (Maybe; just; nothing; fromMaybe)
open import Data.Nat as â„• using (â„•; zero; suc; sâ‰¤s; _âŠ“_; _âˆ¸_; _+_; _<áµ‡_; _â‰¤áµ‡_)
open import Data.Nat.Properties using (1+nâ‰°n; â‰¤-trans; +-identityÊ³; +-comm; +-assoc; â‰¤â‡’â‰¯; mâ‰¤m+n; â‰°â‡’>; â‰®â‡’â‰¥)
open import Data.Product using (Î£; _,_; âˆƒ; Î£-syntax; âˆƒ-syntax)
open import Data.Product using (_Ã—_; projâ‚; projâ‚‚)
import Data.String as String
open import Data.Sum using (_âŠ_; injâ‚; injâ‚‚)
open import Function.Base using (case_of_; _âˆ˜_)
import Relation.Binary.PropositionalEquality as Eq
open Eq using (_â‰¡_; refl; cong; sym)
open import Relation.Nullary using (Dec; yes; no; Â¬_)

open import Marlowe.Language.Contract
open import Marlowe.Language.Input
open import Marlowe.Language.State
open import Marlowe.Language.Transaction
open import Marlowe.Semantics.Evaluate

open import Contrib.Data.List.AssocList
open Decidable _â‰Ÿ-AccountIdÃ—Token_ renaming (_â†‘_ to _â†‘-AccountIdÃ—Token_; _âˆˆ?_ to _âˆˆ?-AccountIdÃ—Token_)
open Decidable _â‰Ÿ-ChoiceId_ renaming (_â€¼_default_ to _â€¼-ChoiceId_default_) using (_âˆˆ?_)
open Decidable _â‰Ÿ-ValueId_ renaming (_â€¼_ to _â€¼-ValueId_; _â€¼_default_ to _â€¼-ValueId_default_; _âˆˆ?_ to _âˆˆ-ValueId?_) hiding (_â†‘_)

open Environment using (timeInterval)
open State using (accounts; boundValues; choices)
open TimeInterval using (startTime)

data ReduceWarning : Set where
  ReduceNonPositivePay : AccountId â†’ Payee â†’ Token â†’ â„¤ â†’ ReduceWarning
  ReducePartialPay : AccountId â†’ Payee â†’ Token â†’ â„• â†’ â„• â†’ ReduceWarning
  ReduceShadowing : ValueId â†’ â„¤ â†’ â„¤ â†’ ReduceWarning
  ReduceAssertionFailed : ReduceWarning

record Configuration : Set where
  constructor âŸª_,_,_,_,_âŸ«
  field contract : Contract
        state : State
        environment : Environment
        warnings : List ReduceWarning
        payments : List Payment

open Configuration

data _â‡€_ : Configuration â†’ Configuration â†’ Set where

  CloseRefund : âˆ€ {a t n s ws ps e}
    --------------------------------
    â†’ âŸª Close
      , record s
          { accounts =
            ((a , t) , n) âˆ· accounts s
          }
      , e
      , ws
      , ps
      âŸ« â‡€
      âŸª Close
      , s
      , e
      , ws
      , a [ t , n ]â†¦ mkParty (unAccountId a) âˆ· ps
      âŸ«

  PayNonPositive : âˆ€ {s e v a p t c ws ps}
    â†’ â„°âŸ¦ v âŸ§ e s â„¤.â‰¤ 0â„¤
    --------------------------------------
    â†’ âŸª Pay a p t v c
      , s
      , e
      , ws
      , ps
      âŸ« â‡€
      âŸª c
      , s
      , e
      , ReduceNonPositivePay a p t (â„°âŸ¦ v âŸ§ e s) âˆ· ws
      , ps
      âŸ«

  PayNoAccount : âˆ€ {s e v a p t c ws ps}
    â†’ â„°âŸ¦ v âŸ§ e s > 0â„¤
    â†’ (a , t) âˆ‰ accounts s
    ------------------------------------
    â†’ âŸª Pay a p t v c
      , s
      , e
      , ws
      , ps
      âŸ« â‡€
      âŸª c
      , s
      , e
      , ReducePartialPay a p t 0 âˆ£ â„°âŸ¦ v âŸ§ e s âˆ£ âˆ· ws -- TODO: proper warning?
      , ps
      âŸ«

  PayInternalTransfer : âˆ€ {s e v aâ‚› aâ‚œ t c ws ps}
    â†’ â„°âŸ¦ v âŸ§ e s > 0â„¤
    â†’ (aâ‚›Ã—tâˆˆas : (aâ‚› , t) âˆˆ accounts s)
    --------------------------------------------
    â†’ let
        m = projâ‚‚ (lookup aâ‚›Ã—tâˆˆas)
        n = âˆ£ â„°âŸ¦ v âŸ§ e s âˆ£
      in
      âŸª Pay aâ‚› (mkAccount aâ‚œ) t v c
      , s
      , e
      , ws
      , ps
      âŸ« â‡€
      âŸª c
      , record s
          { accounts =
            ((aâ‚œ , t) , (m âŠ“ n)) â†‘-update (aâ‚›Ã—tâˆˆas âˆ·= ((aâ‚› , t) , m âˆ¸ n))
          }
      , e
      , if (m <áµ‡ n)
          then ReducePartialPay aâ‚› (mkAccount aâ‚œ) t m n âˆ· ws
          else ws
      , ps
      âŸ«

  PayExternal : âˆ€ {s e v a t c ws ps p}
    â†’ â„°âŸ¦ v âŸ§ e s > 0â„¤
    â†’ (aÃ—tâˆˆas : (a , t) âˆˆ accounts s)
    -----------------------------------
    â†’ let
        m = projâ‚‚ (lookup aÃ—tâˆˆas)
        n = âˆ£ â„°âŸ¦ v âŸ§ e s âˆ£
      in
      âŸª Pay a (mkParty p) t v c
      , s
      , e
      , ws
      , ps
      âŸ« â‡€
      âŸª c
      , record s
          { accounts =
            aÃ—tâˆˆas âˆ·= ((a , t) , m âˆ¸ n)
          }
      , e
      , if (m <áµ‡ n)
          then ReducePartialPay a (mkParty p) t m n âˆ· ws
          else ws
      , a [ t , m âŠ“ n ]â†¦ mkParty p âˆ· ps
      âŸ«

  IfTrue : âˆ€ {s e o câ‚ câ‚‚ ws ps}
    â†’ ğ’ªâŸ¦ o âŸ§ e s â‰¡ true
    ----------------------------
    â†’ âŸª If o câ‚ câ‚‚
      , s
      , e
      , ws
      , ps
      âŸ« â‡€
      âŸª câ‚
      , s
      , e
      , ws
      , ps
      âŸ«

  IfFalse : âˆ€ {s e o câ‚ câ‚‚ ws ps}
    â†’ ğ’ªâŸ¦ o âŸ§ e s â‰¡ false
    -----------------------------
    â†’ âŸª If o câ‚ câ‚‚
      , s
      , e
      , ws
      , ps
      âŸ« â‡€
      âŸª câ‚‚
      , s
      , e
      , ws
      , ps
      âŸ«

  WhenTimeout : âˆ€ {s t tâ‚› Î”â‚œ c ws ps cs}
    â†’ t â„•.â‰¤ tâ‚›
    -----------------------------------
    â†’ let
        e = mkEnvironment (mkInterval (mkPosixTime tâ‚›) Î”â‚œ)
      in
      âŸª When cs (mkTimeout (mkPosixTime t)) c
      , s
      , e
      , ws
      , ps
      âŸ« â‡€
      âŸª c
      , s
      , e
      , ws
      , ps
      âŸ«

  LetShadow : âˆ€ {s e c i v váµ¢ ws ws' ps}
    â†’ (i , váµ¢) âˆˆ-List boundValues s
    â†’ ws' â‰¡ ReduceShadowing i váµ¢ (â„°âŸ¦ v âŸ§ e s) âˆ· ws
    ----------------------------------------------
    â†’ âŸª Let i v c
      , s
      , e
      , ws
      , ps
      âŸ« â‡€
      âŸª c
      , s
      , e
      , ws'
      , ps
      âŸ«

  LetNoShadow : âˆ€ {s e c i v ws ps}
    â†’ i âˆ‰ boundValues s
    --------------------
    â†’ âŸª Let i v c
      , s
      , e
      , ws
      , ps
      âŸ« â‡€
      âŸª c
      , record s
          { boundValues =
            (i , â„°âŸ¦ v âŸ§ e s) âˆ· boundValues s
          }
      , e
      , ws
      , ps
      âŸ«

  AssertTrue : âˆ€ {s e o c ws ps}
    â†’ ğ’ªâŸ¦ o âŸ§ e s â‰¡ true
    ----------------------------
    â†’ âŸª Assert o c
      , s
      , e
      , ws
      , ps
      âŸ« â‡€
      âŸª c
      , s
      , e
      , ws
      , ps
      âŸ«

  AssertFalse : âˆ€ {s e o c ws ps}
    â†’ ğ’ªâŸ¦ o âŸ§ e s â‰¡ false
    -----------------------------
    â†’ âŸª Assert o c
      , s
      , e
      , ws
      , ps
      âŸ« â‡€
      âŸª c
      , s
      , e
      , ReduceAssertionFailed âˆ· ws
      , ps
      âŸ«


-- reflexive and transitive closure
infix  2 _â‡€â‹†_
infix  1 begin_
infixr 2 _â‡€âŸ¨_âŸ©_
infix  3 _âˆ

data _â‡€â‹†_ : Configuration â†’ Configuration â†’ Set where
  _âˆ : âˆ€ M
      ------
    â†’ M â‡€â‹† M

  _â‡€âŸ¨_âŸ©_ : âˆ€ L {M N}
    â†’ L â‡€ M
    â†’ M â‡€â‹† N
      ------
    â†’ L â‡€â‹† N

begin_ : âˆ€ {M N}
  â†’ M â‡€â‹† N
    ------
  â†’ M â‡€â‹† N
begin Mâ‡€â‹†N = Mâ‡€â‹†N

data Quiescent : Configuration â†’ Set where

  close : âˆ€ {e cs vs ws m ps}
    -------------------------
    â†’ Quiescent
        âŸª Close
        , âŸ¨ [] , cs , vs , m âŸ©
        , e
        , ws
        , ps
        âŸ«

  waiting : âˆ€ {t tâ‚› Î”â‚œ cs s c ws ps}
    â†’ (tâ‚› + Î”â‚œ) â„•.< t
    -------------------------------
    â†’ Quiescent
        âŸª When cs (mkTimeout (mkPosixTime t)) c
        , s
        , mkEnvironment (mkInterval (mkPosixTime tâ‚›) Î”â‚œ)
        , ws
        , ps
        âŸ«

data AmbiguousTimeInterval : Configuration â†’ Set where

  AmbiguousTimeIntervalError : âˆ€ {t tâ‚› Î”â‚œ cs c s ws ps}
    â†’ tâ‚› â„•.< t
    â†’ (tâ‚› + Î”â‚œ) â„•.â‰¥ t
    --------------------------------------------------
    â†’ AmbiguousTimeInterval
        âŸª When cs (mkTimeout (mkPosixTime t)) c
        , s
        , mkEnvironment (mkInterval (mkPosixTime tâ‚›) Î”â‚œ)
        , ws
        , ps
        âŸ«

data Reducible (C : Configuration) : Set where

  step : âˆ€ {D}
    â†’ C â‡€ D
      -----------
    â†’ Reducible C

  quiescent :
      Quiescent C
      -----------
    â†’ Reducible C

  ambiguousTimeInterval :
      AmbiguousTimeInterval C
      -----------------------
    â†’ Reducible C


progress : âˆ€ (C : Configuration) â†’ Reducible C
progress
  âŸª Close
  , âŸ¨ [] , _ , _ , _ âŸ©
  , _
  , _
  , _
  âŸ« = quiescent close
progress
  âŸª Close
  , âŸ¨ _ âˆ· _ , _ , _ , _ âŸ©
  , _
  , _
  , _
  âŸ« = step CloseRefund
progress
  âŸª Pay a (mkAccount p) t v c
  , s@(âŸ¨ as , _ , _ , _ âŸ©)
  , e
  , _
  , _
  âŸ« with â„°âŸ¦ v âŸ§ e s â‰¤? 0â„¤ | (a , t) âˆˆ?-AccountIdÃ—Token as
... | yes vâ‰¤0 | _           = step (PayNonPositive vâ‰¤0)
... | no  vâ‰°0 | yes aÃ—tâˆˆas = step (PayInternalTransfer (â„¤.â‰°â‡’> vâ‰°0) aÃ—tâˆˆas)
... | no  vâ‰°0 | no Â¬aÃ—tâˆˆas = step (PayNoAccount (â„¤.â‰°â‡’> vâ‰°0) (Â¬Anyâ‡’AllÂ¬ as Â¬aÃ—tâˆˆas))
progress
  âŸª Pay a (mkParty p) t v _
  , s@(âŸ¨ as , _ , _ , _ âŸ©)
  , e
  , _
  , _
  âŸ« with â„°âŸ¦ v âŸ§ e s â‰¤? 0â„¤ | (a , t) âˆˆ?-AccountIdÃ—Token as
... | yes vâ‰¤0 | _           = step (PayNonPositive vâ‰¤0)
... | no  vâ‰°0 | yes aÃ—tâˆˆas = step (PayExternal (â„¤.â‰°â‡’> vâ‰°0) aÃ—tâˆˆas)
... | no  vâ‰°0 | no Â¬aÃ—tâˆˆas = step (PayNoAccount (â„¤.â‰°â‡’> vâ‰°0) (Â¬Anyâ‡’AllÂ¬ as Â¬aÃ—tâˆˆas))
progress
  âŸª If o câ‚ câ‚‚
  , s
  , e
  , _
  , _
  âŸ« with ğ’ªâŸ¦ o âŸ§ e s â‰Ÿ true
... | yes oâ‰¡true = step (IfTrue oâ‰¡true)
... | no Â¬oâ‰¡true = step (IfFalse (Â¬-not Â¬oâ‰¡true))
progress
  âŸª When cs (mkTimeout (mkPosixTime t)) c
  , _
  , mkEnvironment (mkInterval (mkPosixTime tâ‚›) Î”â‚œ)
  , _
  , _
  âŸ« with (tâ‚› + Î”â‚œ) â„•.<? t | t â„•.â‰¤? tâ‚›
... | yes tâ‚‘<t | _        = quiescent (waiting tâ‚‘<t)
... | _        | yes tâ‰¤tâ‚› = step (WhenTimeout tâ‰¤tâ‚›)
... | no Â¬tâ‚‘<t | no Â¬tâ‰¤tâ‚›  = ambiguousTimeInterval (AmbiguousTimeIntervalError (â‰°â‡’> Â¬tâ‰¤tâ‚›) (â‰®â‡’â‰¥ Â¬tâ‚‘<t))
progress
  âŸª Let i v c , s@(âŸ¨ _ , _ , vs , _ âŸ©)
  , e
  , ws
  , ps
  âŸ« with i âˆˆ-ValueId? vs
... | yes iâˆˆvs =
  let váµ¢ = projâ‚‚ (lookup iâˆˆvs)
  in step (LetShadow {s} {e} {c} {i} {v} {váµ¢} {ws} {ReduceShadowing i váµ¢ (â„°âŸ¦ v âŸ§ e s) âˆ· ws} {ps} (lookupâˆˆ-L iâˆˆvs) refl)
  where
    lookupâˆˆ-L : âˆ€ {A B : Set} {a : A} {abs : AssocList A B}
      â†’ (aâˆˆabs : a âˆˆ abs)
      â†’ (a , projâ‚‚ (lookup aâˆˆabs)) âˆˆ-List abs
    lookupâˆˆ-L (here refl) = here refl
    lookupâˆˆ-L (there aâˆˆabs) = there (lookupâˆˆ-L aâˆˆabs)
... | no Â¬aâˆˆabs = step (LetNoShadow (Â¬Anyâ‡’AllÂ¬ vs Â¬aâˆˆabs))
progress
  âŸª Assert o c
  , s
  , e
  , _
  , _
  âŸ« with ğ’ªâŸ¦ o âŸ§ e s â‰Ÿ true
... | yes oâ‰¡true = step (AssertTrue oâ‰¡true)
... | no Â¬oâ‰¡true = step (AssertFalse (Â¬-not Â¬oâ‰¡true))

data _â‡€â‚™_ : Configuration â†’ Configuration â†’ Set where

  Reduce-until-quiescent : âˆ€ {C D}
    â†’ C â‡€â‹† D
    â†’ Quiescent D
    -------------
    â†’ C â‡€â‚™ D

  Ambiguous-time-interval : âˆ€ {C D}
    â†’ C â‡€â‹† D
    â†’ AmbiguousTimeInterval D
    -------------------------
    â†’ C â‡€â‚™ D

  Execution-budget-exceeded : âˆ€ {C D}
    â†’ C â‡€â‹† D
    ---------
    â†’ C â‡€â‚™ D

-- Evaluator

eval :
  âˆ€ (C : Configuration)
  â†’ â„•
  â†’ Î£[ D âˆˆ Configuration ] (C â‡€â‚™ D)
eval C zero = C , Execution-budget-exceeded (C âˆ)
eval C (suc m) with progress C
... | quiescent q = C , Reduce-until-quiescent (C âˆ) q
... | ambiguousTimeInterval a = C , Ambiguous-time-interval (C âˆ) a
... | step {D} Câ‡€D with eval D m
...      | E , Reduce-until-quiescent Dâ‡€â‹†E s = E , Reduce-until-quiescent (C â‡€âŸ¨ Câ‡€D âŸ© Dâ‡€â‹†E) s
...      | E , Ambiguous-time-interval Dâ‡€â‹†E a = E , Ambiguous-time-interval (C â‡€âŸ¨ Câ‡€D âŸ© Dâ‡€â‹†E) a
...      | E , Execution-budget-exceeded Dâ‡€â‹†E = E , Execution-budget-exceeded (C â‡€âŸ¨ Câ‡€D âŸ© Dâ‡€â‹†E)

-- Examples

private
  roleâ‚ roleâ‚‚ : Party
  roleâ‚ = Role (mkByteString "foo")
  roleâ‚‚ = Role (mkByteString "bar")

  accountIdâ‚ accountIdâ‚‚ : AccountId
  accountIdâ‚ = mkAccountId roleâ‚
  accountIdâ‚‚ = mkAccountId roleâ‚‚

  tokenâ‚ : Token
  tokenâ‚ = mkToken (mkByteString "") (mkByteString "")

  configâ‚‚ : Configuration
  configâ‚‚ =
    âŸª If TrueObs Close Close
    , âŸ¨ [ (accountIdâ‚ , tokenâ‚ ) , 5 ] , [] , [] , mkPosixTime 0 âŸ©
    , mkEnvironment (mkInterval (mkPosixTime 0) 5)
    , []
    , []
    âŸ«

  configâ‚ : Configuration
  configâ‚ =
    âŸª Close
    , âŸ¨ [ ( accountIdâ‚ , tokenâ‚ ) , 5 ] , [] , [] , mkPosixTime 0 âŸ©
    , mkEnvironment (mkInterval (mkPosixTime 0) 5)
    , []
    , []
    âŸ«

  configâ‚€ : Configuration
  configâ‚€ =
    âŸª Close
    , âŸ¨ [] , [] , [] , mkPosixTime 0 âŸ©
    , mkEnvironment (mkInterval (mkPosixTime 0) 5)
    , []
    , [ accountIdâ‚ [ tokenâ‚ , 5 ]â†¦ mkParty (unAccountId accountIdâ‚) ]
    âŸ«

  _ = eval configâ‚‚ 100 â‰¡ (configâ‚€ , Reduce-until-quiescent (configâ‚‚ â‡€âŸ¨ IfTrue refl âŸ© configâ‚ â‡€âŸ¨ CloseRefund âŸ© configâ‚€ âˆ) close)
