---
title: Marlowe.Semantics.Operate
layout: page
---

The module contains the formalisation of mid-step and big-step semantics for Marlowe.

```
open import Relation.Binary using (DecidableEquality)

module Marlowe.Semantics.Operate
  {Party : Set} (_â‰Ÿ-Party_ : DecidableEquality Party)
  {Token : Set} (_â‰Ÿ-Token_ : DecidableEquality Token)
  where
```

## Imports

```
open import Data.Bool as ğ”¹ using (Bool; if_then_else_; not; _âˆ§_; _âˆ¨_; true; false)
open import Data.Integer as â„¤ using (âˆ£_âˆ£; +_)
open import Data.List using (List; []; _âˆ·_; _++_; foldr; reverse; [_]; null; map)
open import Data.List.Membership.Propositional using (_âˆˆ_)
open import Data.List.Membership.DecSetoid using () renaming (_âˆˆ?_ to _âˆˆ?-List_)
open import Data.List.Relation.Unary.Any using (Any; here; there; lookup; any?)
open import Data.Maybe using (Maybe; just; nothing; fromMaybe)
open import Data.Nat as â„• using (â„•; suc; zero; _<_; _<áµ‡_; _<?_; _â‰Ÿ_; zâ‰¤n; sâ‰¤s; _+_; _âŠ”_; _âˆ¸_; _â‰¥_)
open import Data.Nat.Properties using (â‰°â‡’>; â‰®â‡’â‰¥; â‰¤-reflexive; â‰¤-trans)
open import Data.Product using (Î£; _,_; âˆƒ; Î£-syntax; âˆƒ-syntax; _Ã—_; projâ‚; projâ‚‚)
open import Data.Sum using (_âŠ_; injâ‚; injâ‚‚)
open import Function.Base using (_âˆ˜_ ; id)
open import Relation.Nullary using (Dec; yes; no; Â¬_)
open import Relation.Nullary.Decidable using (âŒŠ_âŒ‹)

open import Relation.Binary using (Decidable; DecidableEquality)
import Relation.Binary.PropositionalEquality as Eq
open Eq using (_â‰¡_; refl; cong; sym; trans)

open import Marlowe.Language
open PartyParam Party
open TokenParam Token
open Equality _â‰Ÿ-Party_ _â‰Ÿ-Token_

open import Marlowe.Semantics.Evaluate _â‰Ÿ-Party_ _â‰Ÿ-Token_
open import Marlowe.Semantics.Reduce _â‰Ÿ-Party_ _â‰Ÿ-Token_

open import Contrib.Data.List.AssocList renaming (_âˆˆ_ to _âˆˆâ€²_)
open Decidable _â‰Ÿ-ChoiceId_

open Configuration
open TokenParam.State
open PosixTime
open TransactionInput
```

# Mid-step semantics

## Waiting

The contract is in the waiting state, when the timeout in the `When` constructor
is after the upper bound of the time interval. In the waiting state the contract
can accept inputs.

```
data Waiting : Configuration â†’ Set where

  waiting : âˆ€ {cs t c s e ws ps}
    â†’ interval-end e < t
    -------------------------------------------
    â†’ Waiting
        âŸª When cs (mkTimeout (mkPosixTime t)) c
        , s
        , e
        , ws
        , ps
        âŸ«
```

## Mid-step reduction rules

Mid-step reduction applies an input to a contract in the waiting state

```
data _â‡’_ : {C : Configuration} â†’ Waiting C Ã— Input â†’ Configuration â†’ Set where

  Deposit : âˆ€ {a p t v n câ‚ e s ws ps cs c tâ‚’ D}
    â†’ (mkCase (Deposit a p t v) câ‚) âˆˆ cs
    â†’ â„°âŸ¦ v âŸ§ e s â‰¡ + n
    â†’ (tâ‚‘<tâ‚’ : interval-end e < tâ‚’)
    â†’ Quiescent D
    â†’ âŸª câ‚
      , record s
          { accounts =
            ((a , t) , n) â†‘-update (accounts s)
          }
      , e
      , ws
      , ps
      âŸ«
      â‡€â‹† D
    -------------------------------------------------
    â†’ ( waiting {cs} {tâ‚’} {c} {s} {e} {ws} {ps} tâ‚‘<tâ‚’
      , NormalInput (IDeposit a p t n)
      ) â‡’ D

  Choice : âˆ€ {câ‚ c i n s bs ws ps cs tâ‚’ e D}
    â†’ (mkCase (Choice i bs) câ‚) âˆˆ cs
    â†’ n inBounds bs â‰¡ true
    â†’ (tâ‚‘<tâ‚’ : interval-end e < tâ‚’)
    â†’ Quiescent D
    â†’ âŸª câ‚
      , record s
          { choices =
            (i , unChosenNum n) â†‘ (choices s)
          }
      , e
      , ws
      , ps
      âŸ«
      â‡€â‹† D
    -------------------------------------------------
    â†’ ( waiting {cs} {tâ‚’} {c} {s} {e} {ws} {ps} tâ‚‘<tâ‚’
      , NormalInput (IChoice i n)
      ) â‡’ D

  Notify : âˆ€ {câ‚ c s o cs tâ‚’ e ws ps D}
    â†’ (mkCase (Notify o) câ‚) âˆˆ cs
    â†’ ğ’ªâŸ¦ o âŸ§ e s â‰¡ true
    â†’ (tâ‚‘<tâ‚’ : interval-end e < tâ‚’)
    â†’ Quiescent D
    â†’ âŸª câ‚ , s , e , ws , ps âŸ« â‡€â‹† D
    -------------------------------------------------
    â†’ ( waiting {cs} {tâ‚’} {c} {s} {e} {ws} {ps} tâ‚‘<tâ‚’
      , NormalInput INotify
      ) â‡’ D
```

The following data type relates `InputContent` with `Action`. This is necessary,
as an `Action` can contain `Observation`s and `Value`s as opposed to the
`InputContent` that has ğ”¹ and â„•.

Evaluation of `Observation` and `Value` requires `State` and `Environment`.

```
data _â†¦_ {s : State} {e : Environment} : InputContent â†’ Action â†’ Set where

  deposit-input : âˆ€ {a p t v n}
    â†’ â„°âŸ¦ v âŸ§ e s â‰¡ + n
    â†’ IDeposit a p t n â†¦ Deposit a p t v

  choice-input : âˆ€ {i n bs}
    â†’ n inBounds bs â‰¡ true
    â†’ IChoice i n â†¦ Choice i bs

  notify-input : âˆ€ {o}
    â†’ ğ’ªâŸ¦ o âŸ§ e s â‰¡ true
    â†’ INotify â†¦ Notify o
```

The function `applicable?` checks, if an `InputContent` can trigger a given `Action`. If
this is the case, the relation is returned.

```
applicable? : âˆ€ {s : State} {e : Environment} â†’ (i : InputContent) â†’ (a : Action) â†’ Maybe (_â†¦_ {s} {e} i a)
```
* IDeposit
```
applicable? {s} {e} (IDeposit aâ‚ pâ‚ tâ‚ n) (Deposit aâ‚‚ pâ‚‚ tâ‚‚ v)
  with aâ‚ â‰Ÿ-AccountId aâ‚‚ | pâ‚ â‰Ÿ-Party pâ‚‚ | tâ‚ â‰Ÿ-Token tâ‚‚ | â„°âŸ¦ v âŸ§ e s  â„¤.â‰Ÿ + n
... | yes refl | yes refl | yes refl | yes p = just (deposit-input {_} {_} {aâ‚} {pâ‚} {tâ‚} {v} {n} p)
... | _        | _        | _        | _     = nothing
applicable? (IDeposit _ _ _ _) (Choice _ _ ) = nothing
applicable? (IDeposit _ _ _ _) (Notify _) = nothing
```
* IChoice
```
applicable? (IChoice _ _ ) (Deposit _ _ _ _ ) = nothing
applicable? (IChoice iâ‚ n) (Choice iâ‚‚ b)
  with iâ‚ â‰Ÿ-ChoiceId iâ‚‚ | n inBounds b ğ”¹.â‰Ÿ true
... | yes refl | yes p = (just (choice-input {_} {_} {iâ‚} {n} {b} p))
... | _        | _     = nothing
applicable? (IChoice _ _) (Notify _) = nothing
```
* INotify
```
applicable? INotify (Deposit _ _ _ _) = nothing
applicable? INotify (Choice _ _) = nothing
applicable? {s} {e} INotify (Notify o)
  with ğ’ªâŸ¦ o âŸ§ e s ğ”¹.â‰Ÿ true
... | yes p = just (notify-input {_} {_} {o = o} p)
... | no _  = nothing
```

## Evaluator for mid-step semantics

```
{-# TERMINATING #-} -- TODO: use sized types instead
â‡’-eval :
  âˆ€ {C : Configuration}
  â†’ (w : Waiting C)
  â†’ (i : Input)
  â†’ (Î£[ D âˆˆ Configuration ] ((w , i) â‡’ D)) âŠ TransactionError
â‡’-eval {âŸª When [] (mkTimeout (mkPosixTime tâ‚’)) c , s , e , ws , ps âŸ«} (waiting tâ‚‘<t) (NormalInput ic) = injâ‚‚ TEApplyNoMatchError

â‡’-eval (waiting {mkCase a câ‚ âˆ· cs} {t} {c} {s} {e} {ws} {ps} tâ‚‘<t) (NormalInput ic)
  with applicable? {s} {e} ic a
```
* here
```
â‡’-eval (waiting {mkCase _ câ‚ âˆ· cs} {_} {_} {s} {e} {ws} {ps} tâ‚‘<t) (NormalInput ic) | just (deposit-input {a} {p} {t} {_} {n} â„°âŸ¦vâŸ§â‰¡+n)
  with â‡€-eval âŸª câ‚ , record s { accounts = ((a , t) , n) â†‘-update (accounts s) } , e , ws , ps âŸ«
... | D , Câ‡€â‹†D , injâ‚ q = injâ‚ (D , Deposit (here refl) â„°âŸ¦vâŸ§â‰¡+n tâ‚‘<t q Câ‡€â‹†D)
... | _ , _    , injâ‚‚ _ = injâ‚‚ TEAmbiguousTimeIntervalError
â‡’-eval (waiting {mkCase _ câ‚ âˆ· cs} {_} {_} {s} {e} {ws} {ps} tâ‚‘<t) (NormalInput ic) | just (choice-input {i} {n} {bs} p)
  with â‡€-eval âŸª câ‚ , record s { choices = (i , unChosenNum n) â†‘ (choices s) } , e , ws , ps âŸ«
... | D , Câ‡€â‹†D , injâ‚ q = injâ‚ (D , Choice (here refl) p tâ‚‘<t q Câ‡€â‹†D)
... | _ , _    , injâ‚‚ q = injâ‚‚ TEAmbiguousTimeIntervalError
â‡’-eval (waiting {mkCase _ câ‚ âˆ· cs} {_} {_} {s} {e} {ws} {ps} tâ‚‘<t) (NormalInput ic) | just (notify-input {o} oâ‰¡true)
  with â‡€-eval âŸª câ‚ , s , e , ws , ps âŸ«
... | D , Câ‡€â‹†D , injâ‚ q = injâ‚ (D , Notify {s = s} {o = o} {e = e} (here refl) oâ‰¡true tâ‚‘<t q Câ‡€â‹†D)
... | _ , _    , injâ‚‚ _ = injâ‚‚ TEAmbiguousTimeIntervalError
```
* there
```
â‡’-eval (waiting {(_ âˆ· cs)} {_} {c} tâ‚‘<t) i@(NormalInput (IDeposit x xâ‚ xâ‚‚ xâ‚ƒ)) | nothing
  with â‡’-eval (waiting {cs} {_} {c} tâ‚‘<t) i
... | injâ‚ (D , (Deposit x xâ‚ xâ‚‚ xâ‚ƒ xâ‚„)) = injâ‚ (D , (Deposit (there x) xâ‚ xâ‚‚ xâ‚ƒ xâ‚„))
... | injâ‚‚ e = injâ‚‚ e
â‡’-eval (waiting {(_ âˆ· cs)} {_} {c} {s} tâ‚‘<t) i@(NormalInput (IChoice x xâ‚)) | nothing
  with â‡’-eval (waiting {cs} {_} {c} {s} tâ‚‘<t) i
... | injâ‚ (D , (Choice x xâ‚ xâ‚‚ xâ‚ƒ xâ‚„)) = injâ‚ (D , (Choice (there x) xâ‚ xâ‚‚ xâ‚ƒ xâ‚„))
... | injâ‚‚ e = injâ‚‚ e
â‡’-eval (waiting {(_ âˆ· cs)} {_} {c} tâ‚‘<t) i@(NormalInput INotify) | nothing
  with â‡’-eval (waiting {cs} {_} {c} tâ‚‘<t) i
... | injâ‚ (D , (Notify x xâ‚ xâ‚‚ xâ‚ƒ xâ‚„)) = injâ‚ (D , (Notify (there x) xâ‚ xâ‚‚ xâ‚ƒ xâ‚„))
... | injâ‚‚ e = injâ‚‚ e
```

# Big-step semantics

## Fix interval

```
data _â†_ : Configuration â†’ Configuration â†’ Set where

  trim-interval : âˆ€ {c as cs bs tâ‚˜ tâ‚› Î”â‚œ ws ps }
    â†’ tâ‚› + Î”â‚œ â‰¥ tâ‚˜
    â†’ âŸª c
      , âŸ¨ as , cs , bs , mkPosixTime tâ‚˜ âŸ©
      , mkEnvironment (mkInterval (mkPosixTime tâ‚›) Î”â‚œ)
      , ws
      , ps
      âŸ«
      â†
      âŸª c
      , âŸ¨ as , cs , bs , mkPosixTime (tâ‚› âŠ” tâ‚˜) âŸ©
      , mkEnvironment (mkInterval (mkPosixTime (tâ‚› âŠ” tâ‚˜)) (Î”â‚œ âˆ¸ (tâ‚˜ âˆ¸ tâ‚›)))
      , ws
      , ps
      âŸ«
```

```
data FixInterval (B : Configuration) : Set where

  trim : âˆ€ {C}
    â†’ B â† C
      -------------
    â†’ FixInterval B

  error :
      interval-end (environment B) < getPosixTime (minTime (state B))
    â†’ FixInterval B
```

```
fixInterval : âˆ€ (B : Configuration) â†’ FixInterval B
fixInterval âŸª _ , âŸ¨ _ , _ , _ , mkPosixTime tâ‚˜ âŸ© , mkEnvironment (mkInterval (mkPosixTime tâ‚›) Î”â‚œ) , _ , _ âŸ« with tâ‚› + Î”â‚œ <? tâ‚˜
... | yes p = error p
... | no p = trim (trim-interval (â‰®â‡’â‰¥ p))
```

## Warnings

```
convertReduceWarnings : List ReduceWarning -> List TransactionWarning
convertReduceWarnings = map convertReduceWarning
  where
    convertReduceWarning : ReduceWarning â†’ TransactionWarning
    convertReduceWarning (ReduceNonPositivePay a p t v) = TransactionNonPositivePay a p t v
    convertReduceWarning (ReducePartialPay a p t v e) = TransactionPartialPay a p t v e
    convertReduceWarning (ReducePayNoAccount a p t v) = TransactionPayNoAccount a p t v
    convertReduceWarning (ReduceShadowing i o n) = TransactionShadowing i o n
    convertReduceWarning ReduceAssertionFailed = TransactionAssertionFailed
```

## Big-step reduction rules

### Result

The result of a big-step includes all the transaction warnings,
all the payments triggered during the execution of the contract
together with the final state.

```
record Result : Set where
  constructor âŸ¦_,_,_âŸ§
  field
    warnings : List TransactionWarning
    payments : List Payment
    state : State
```

### Rules

The rules for big-step semantics cover the following steps
* Reduce a contract until quiescent
* Applying an input to a contract
* Closing the contract

```
data _â‡“_ : Contract Ã— State â†’ Result â†’ Set where

  reduce-until-quiescent : âˆ€ {C D ws ps s}
    â†’ warnings C â‰¡ []
    â†’ payments C â‰¡ []
    â†’ C â‡€â‹† D
    â†’ Quiescent D
    â†’ (contract D , state D) â‡“
      âŸ¦ ws
      , ps
      , s
      âŸ§
      -----------------------------------------
    â†’ (contract C , state C) â‡“
      âŸ¦ ws ++ convertReduceWarnings (warnings D)
      , ps ++ payments D
      , s
      âŸ§

  apply-input : âˆ€ {i cs t c s e ws ps C D wsâ€² psâ€² sâ€²}
    â†’ (tâ‚‘<t : interval-end (environment C) < t)
    â†’ âŸª When cs (mkTimeout (mkPosixTime t)) c , s , e , ws , ps âŸ« â† C
    â†’ (waiting {cs} {t} {c} {state C} {environment C} {ws} {ps} tâ‚‘<t , i) â‡’ D
    â†’ (contract D , state D) â‡“
      âŸ¦ wsâ€²
      , psâ€²
      , sâ€²
      âŸ§
      ---------------------------------------------
    â†’ (When cs (mkTimeout (mkPosixTime t)) c , s) â‡“
      âŸ¦ wsâ€² ++ convertReduceWarnings (warnings D)
      , psâ€² ++ payments D
      , sâ€²
      âŸ§

  done : âˆ€ {s}
    â†’ accounts s â‰¡ []
      ---------------
    â†’ (Close , s) â‡“
      âŸ¦ []
      , []
      , s
      âŸ§
```

## Evaluator for big step-semantics

```
{-# TERMINATING #-} -- TODO: use sized types instead
â‡“-eval :
  âˆ€ (c : Contract)
  â†’ (s : State)
  â†’ List TransactionInput
  â†’ (Î£[ r âˆˆ Result ] (c , s) â‡“ r) âŠ TransactionError
```

* Close

```
â‡“-eval Close s@(âŸ¨ [] , _ , _ , _ âŸ©) [] = injâ‚ (âŸ¦ [] , [] , s âŸ§ , done refl)
```

* When

```
â‡“-eval
  (When cs (mkTimeout (mkPosixTime t)) c) s@(âŸ¨ _ , _ , _ , mkPosixTime tâ‚˜ âŸ©) ((mkTransactionInput i@(mkInterval (mkPosixTime tâ‚›) Î”â‚œ) _) âˆ· is)
    with fixInterval âŸª When cs (mkTimeout (mkPosixTime t)) c , s , mkEnvironment i , [] , [] âŸ«
... | error _ = injâ‚‚ (TEIntervalError (IntervalInPastError (mkPosixTime tâ‚˜) i))
... | trim {C} Bâ†C
    with interval-end (environment C) <? t
... | no tâ‰¤tâ‚‘
    with â‡€-eval âŸª When cs (mkTimeout (mkPosixTime t)) c , s , mkEnvironment i , [] , [] âŸ«
... | _ , _ , injâ‚‚ _ = injâ‚‚ TEAmbiguousTimeIntervalError
... | D , CÃ—iâ‡’D , injâ‚ q
    with â‡“-eval (contract D) (state D) is
... | injâ‚ (âŸ¦ ws , ps , s âŸ§ , dÃ—sÃ—isâ‡“r) = injâ‚ (âŸ¦ ws ++ convertReduceWarnings (warnings D) , ps ++ payments D , s âŸ§ , reduce-until-quiescent refl refl CÃ—iâ‡’D q dÃ—sÃ—isâ‡“r)
... | injâ‚‚ e = injâ‚‚ e

â‡“-eval
  (When cs (mkTimeout (mkPosixTime t)) c) s ((mkTransactionInput i@(mkInterval (mkPosixTime tâ‚›) Î”â‚œ) []) âˆ· is)
    | trim Bâ†C | yes tâ‚‘<t
    with â‡€-eval âŸª When cs (mkTimeout (mkPosixTime t)) _ , s , mkEnvironment i , [] , [] âŸ«
... | _ , _ , injâ‚‚ _ = injâ‚‚ TEAmbiguousTimeIntervalError
... | D , CÃ—iâ‡’D , injâ‚ q
    with â‡“-eval (contract D) (state D) is
... | injâ‚ (âŸ¦ ws , ps , s âŸ§ , dÃ—sÃ—isâ‡“r) = injâ‚ (âŸ¦ ws ++ convertReduceWarnings (warnings D) , ps ++ payments D , s âŸ§ , reduce-until-quiescent refl refl CÃ—iâ‡’D q dÃ—sÃ—isâ‡“r)
... | injâ‚‚ e = injâ‚‚ e

â‡“-eval
  (When cs (mkTimeout (mkPosixTime t)) c) (âŸ¨ as , css , bs , mkPosixTime tâ‚˜ âŸ©) ((mkTransactionInput i@(mkInterval (mkPosixTime tâ‚›) Î”â‚œ) (ts âˆ· tss)) âˆ· is)
    | trim {C} Bâ†C | yes tâ‚‘<t
    with â‡’-eval (waiting {cs} {t} {c} {state C} {environment C} {[]} {[]} tâ‚‘<t) ts
... | injâ‚‚ _ = injâ‚‚ TEUselessTransaction
... | injâ‚ (D , CÃ—iâ‡’D)
    with â‡“-eval (contract D) (state D) is
... | injâ‚ (âŸ¦ ws , ps , sâ€² âŸ§ , dÃ—sÃ—isâ‡“r) = injâ‚ (âŸ¦ ws ++ convertReduceWarnings (warnings D) , ps ++ payments D , sâ€² âŸ§ , apply-input {ts} {cs} {t} {c} {_} {e = mkEnvironment i} {[]} {[]} tâ‚‘<t Bâ†C CÃ—iâ‡’D dÃ—sÃ—isâ‡“r)
... | injâ‚‚ e = injâ‚‚ e
```

* Otherwise

```
â‡“-eval c s []
    with â‡€-eval âŸª c , s , mkEnvironment (mkInterval (mkPosixTime 0) 0) , [] , [] âŸ«
... | _ , _ , injâ‚‚ _ = injâ‚‚ TEAmbiguousTimeIntervalError
... | D , CÃ—iâ‡’D , injâ‚ q
    with â‡“-eval (contract D) (state D) []
... | injâ‚ (âŸ¦ ws , ps , s âŸ§ , dÃ—sÃ—isâ‡“r) = injâ‚ (âŸ¦ ws ++ convertReduceWarnings (warnings D) , ps ++ payments D , s âŸ§ , reduce-until-quiescent refl refl CÃ—iâ‡’D q dÃ—sÃ—isâ‡“r)
... | injâ‚‚ e = injâ‚‚ e

â‡“-eval c s ((mkTransactionInput i _) âˆ· is)
    with â‡€-eval âŸª c , s , mkEnvironment i , [] , [] âŸ«
... | _ , _ , injâ‚‚ _ = injâ‚‚ TEAmbiguousTimeIntervalError
... | D , CÃ—iâ‡’D , injâ‚ q
    with â‡“-eval (contract D) (state D) is
... | injâ‚ (âŸ¦ ws , ps , s âŸ§ , dÃ—sÃ—isâ‡“r) = injâ‚ (âŸ¦ ws ++ convertReduceWarnings (warnings D) , ps ++ payments D , s âŸ§ , reduce-until-quiescent refl refl CÃ—iâ‡’D q dÃ—sÃ—isâ‡“r)
... | injâ‚‚ e = injâ‚‚ e
```
