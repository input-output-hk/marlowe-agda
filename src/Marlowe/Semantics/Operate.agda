
module Marlowe.Semantics.Operate where


open import Agda.Builtin.Int using (Int)
open import Data.Bool using (Bool; if_then_else_; not; _âˆ§_; _âˆ¨_; true; false)
open import Data.Integer using (_<?_; _â‰¤?_; _â‰Ÿ_ ; _âŠ”_; _âŠ“_; _+_; _-_; +_; 0â„¤ ; _â‰¤_ ; _>_ ; _â‰¥_ ; _<_; âˆ£_âˆ£)
open import Data.List using (List; []; _âˆ·_; _++_; foldr; reverse; [_]; null)
open import Data.Maybe using (Maybe; just; nothing; fromMaybe)
open import Data.Nat as â„• using (â„•)
open import Data.Product using (Î£; _,_; âˆƒ; Î£-syntax; âˆƒ-syntax)
open import Data.Product using (_Ã—_; projâ‚; projâ‚‚)
import Data.String as String
open import Function.Base using (case_of_)
open import Marlowe.Language.Contract
open import Marlowe.Language.Input
open import Marlowe.Language.State
open import Marlowe.Language.Transaction
open import Marlowe.Semantics.Evaluate
open import Primitives
open import Relation.Nullary.Decidable using (âŒŠ_âŒ‹)
open import Relation.Nullary using (Dec; yes; no; Â¬_)

import Relation.Binary.PropositionalEquality as Eq
open Eq using (_â‰¡_; refl; cong; sym)

open import Primitives
open Decidable _â‰Ÿ-AccountIdÃ—Token_  renaming (_â€¼_default_ to _â€¼áµƒ_default_; _â†‘_ to _â†‘-AccountIdÃ—Token_) hiding (_âˆˆ?_)
open Decidable _â‰Ÿ-ChoiceId_ renaming (_â€¼_default_ to _â€¼-ChoiceId_default_;  _â†‘_ to _â†‘-ChoiceId_) using (_âˆˆ?_)
open Decidable _â‰Ÿ-ValueId_ renaming (_â€¼_ to _â€¼_ValueId_; _â€¼_default_ to _â€¼-ValueId_default_; _âˆˆ?_ to _âˆˆ-ValueId?_; _â†‘_ to _â†‘-ValueId_)

Accounts : Set
Accounts = AssocList (AccountId Ã— Token) â„•

fixInterval : TimeInterval â†’ State â†’ IntervalResult
fixInterval interval state =
  let
    mkInterval (mkPosixTime low) delta = interval
    high = low â„•.+ delta
  in
    if âŒŠ high â„•.<? low âŒ‹
      then mkIntervalError (InvalidInterval interval)
      else
        let
          curMinTime = State.minTime state
          newLow = low â„•.âŠ” PosixTime.getPosixTime curMinTime
          curInterval = record interval {startTime = mkPosixTime newLow}
          env = record {timeInterval = curInterval}
          newState = record state {minTime = mkPosixTime newLow}
        in
          if âŒŠ high â„•.<? PosixTime.getPosixTime curMinTime âŒ‹
            then mkIntervalError (IntervalInPastError curMinTime interval)
            else IntervalTrimmed env newState


refundOne : AssocList (AccountId Ã— Token) â„• â†’ Maybe (Party Ã— Token Ã— â„• Ã— Accounts)
refundOne [] = nothing
refundOne (((mkAccountId p , t) , i) âˆ· a) = just (p , t , i , a)

moneyInAccount : AccountId â†’ Token â†’ Accounts â†’ â„•
moneyInAccount a t as = (a , t) â€¼áµƒ as default 0

updateMoneyInAccount : AccountId â†’ Token â†’ â„• â†’ Accounts â†’ Accounts
updateMoneyInAccount account token amount accounts =
  let
    key = account , token
  in
    (key , amount) â†‘-AccountIdÃ—Token accounts

addMoneyToAccount : AccountId â†’ Token â†’ â„• â†’ Accounts â†’ Accounts
addMoneyToAccount account token amount accounts =
  let
    balance = moneyInAccount account token accounts
    newBalance = balance â„•.+ amount
  in
    updateMoneyInAccount account token newBalance accounts

data ReduceEffect : Set where
  ReduceWithPayment : Payment â†’ ReduceEffect
  ReduceNoPayment : ReduceEffect

data ReduceWarning : Set where
  ReduceNoWarning : ReduceWarning
  ReduceNonPositivePay : AccountId â†’ Payee â†’ Token â†’ Int â†’ ReduceWarning
  ReducePartialPay : AccountId â†’ Payee â†’ Token â†’ â„• â†’ â„• â†’ ReduceWarning
  ReduceShadowing : ValueId â†’ Int â†’ Int â†’ ReduceWarning
  ReduceAssertionFailed : ReduceWarning

data ReduceStepResult : Set where
  Reduced : ReduceWarning â†’ ReduceEffect â†’ State â†’ Contract â†’ ReduceStepResult
  NotReduced : ReduceStepResult
  AmbiguousTimeIntervalReductionError : ReduceStepResult

data ReduceResult : Set where
  ContractQuiescent : Bool â†’ List ReduceWarning â†’ List Payment â†’ State â†’ Contract â†’ ReduceResult
  RRAmbiguousTimeIntervalError : ReduceResult


giveMoney : AccountId â†’ Payee â†’ Token â†’ â„• â†’ Accounts â†’ ReduceEffect Ã— Accounts
giveMoney account payee token amount accounts =
  record {
    fst = ReduceWithPayment (mkPayment account payee token amount);
    snd = newAccounts payee
    }
    where
      newAccounts : Payee â†’ Accounts
      newAccounts payee' with payee'
      ... | mkParty _ = accounts
      ... | mkAccount account' = addMoneyToAccount account' token amount accounts


reduceContractStep : Environment â†’ State â†’ Contract â†’ ReduceStepResult
reduceContractStep env state Close
  with refundOne (State.accounts state)
... | just (party , token , amount , newAccounts) =
       let
         newState = record state {accounts = newAccounts}
       in
         Reduced ReduceNoWarning (ReduceWithPayment (mkPayment (mkAccountId party) (mkParty party) token amount)) newState Close
... | nothing = NotReduced
reduceContractStep env state (Pay accId payee tok val cont) =
  let
    amountToPay = â„°âŸ¦ val âŸ§ env state
  in
    if âŒŠ amountToPay â‰¤? 0â„¤ âŒ‹
      then (
        let
          warning = ReduceNonPositivePay accId payee tok amountToPay
        in
          Reduced warning ReduceNoPayment state cont
      )
      else (
        let
          balance = moneyInAccount accId tok (State.accounts state)
          paidAmount = balance â„•.âŠ“ âˆ£ amountToPay âˆ£
          newBalance = balance â„•.âˆ¸ paidAmount
          newAccs = updateMoneyInAccount accId tok newBalance (State.accounts state)
          warning = if âŒŠ paidAmount â„•.<? âˆ£ amountToPay âˆ£ âŒ‹
                      then ReducePartialPay accId payee tok paidAmount âˆ£ amountToPay âˆ£
                      else ReduceNoWarning
          (payment , finalAccs) = giveMoney accId payee tok paidAmount newAccs
          newState = record state {accounts = finalAccs}
        in
          Reduced warning payment newState cont
      )
reduceContractStep env state (If obs cont1 cont2) =
  let
    cont = if ð’ªâŸ¦ obs âŸ§ env state
             then cont1
             else cont2
  in
    Reduced ReduceNoWarning ReduceNoPayment state cont
reduceContractStep env state (When _ (mkTimeout (mkPosixTime timeout)) cont) =
  let
    interval = Environment.timeInterval env
  in
    if âŒŠ PosixTime.getPosixTime (TimeInterval.startTime interval) â„•.<? timeout âŒ‹
      then NotReduced
      else if âŒŠ timeout â„•.â‰¤? PosixTime.getPosixTime (TimeInterval.startTime interval) âŒ‹
             then Reduced ReduceNoWarning ReduceNoPayment state cont
             else AmbiguousTimeIntervalReductionError
reduceContractStep env state (Let valId val cont) =
  let
    evaluatedValue = â„°âŸ¦ val âŸ§ env state
    boundVals = State.boundValues state
    newState = record state {boundValues = (valId , evaluatedValue) â†‘-ValueId boundVals}
    warn = if âŒŠ valId âˆˆ-ValueId? boundVals âŒ‹
             then ReduceShadowing valId (valId â€¼-ValueId boundVals default 0â„¤) evaluatedValue
             else ReduceNoWarning
  in
    Reduced warn ReduceNoPayment newState cont
reduceContractStep env state (Assert obs cont) =
  let
    warn = if ð’ªâŸ¦ obs âŸ§ env state
             then ReduceNoWarning
             else ReduceAssertionFailed
  in
    Reduced warn ReduceNoPayment state cont


{-# TERMINATING #-}
reduceContractUntilQuiescent : Environment â†’ State â†’ Contract â†’ ReduceResult
reduceContractUntilQuiescent env state contract =
  reductionLoop false [] [] env state contract
    where
      newPayments : List Payment â†’ ReduceEffect â†’ List Payment
      newPayments payments ReduceNoPayment = payments
      newPayments payments (ReduceWithPayment payment) = payment âˆ· payments
      newWarnings : List ReduceWarning â†’ ReduceWarning â†’ List ReduceWarning
      newWarnings warnings ReduceNoWarning = warnings
      newWarnings warnings warning = warning âˆ· warnings
      reductionLoop : Bool â†’ List ReduceWarning â†’ List Payment â†’ Environment â†’ State â†’ Contract â†’ ReduceResult
      reductionLoop reduced warnings payments env state contract
        with reduceContractStep env state contract
      ... | Reduced warning effect newState cont = reductionLoop true (newWarnings warnings warning) (newPayments payments effect) env newState cont
      ... | AmbiguousTimeIntervalReductionError = RRAmbiguousTimeIntervalError
      ... | NotReduced = ContractQuiescent reduced (reverse warnings) (reverse payments) state contract


data ApplyWarning : Set where
  ApplyNoWarning : ApplyWarning
  ApplyNonPositiveDeposit : Party â†’ AccountId â†’ Token â†’ Int â†’ ApplyWarning

data ApplyAction : Set where
  AppliedAction : ApplyWarning â†’ State â†’ ApplyAction
  NotAppliedAction : ApplyAction


applyAction : Environment â†’ State â†’ InputContent â†’ Action â†’ ApplyAction
applyAction env state (IDeposit accId1 party1 tok1 amount) (Deposit accId2 party2 tok2 val) =
  if âŒŠ accId1 â‰Ÿ-AccountId accId2 âŒ‹ âˆ§ âŒŠ party1 â‰Ÿ-Party party2 âŒ‹ âˆ§ âŒŠ tok1 â‰Ÿ-Token tok2 âŒ‹ âˆ§ âŒŠ ((+ amount) â‰Ÿ â„°âŸ¦ val âŸ§ env state) âŒ‹
    then AppliedAction
           ApplyNoWarning
           (
             record state {
               accounts = addMoneyToAccount accId1 tok1 amount (State.accounts state)
             }
           )
    else NotAppliedAction
applyAction _ state (IChoice choId1 choice) (Choice choId2 bounds) =
  if âŒŠ choId1 â‰Ÿ-ChoiceId choId2 âŒ‹ âˆ§ choice inBounds bounds
    then AppliedAction ApplyNoWarning (record state {choices = (choId1 , unChosenNum choice) â†‘-ChoiceId (State.choices state)})
    else NotAppliedAction
applyAction env state INotify (Notify obs) =
  if ð’ªâŸ¦ obs âŸ§ env state
    then AppliedAction ApplyNoWarning state
    else NotAppliedAction
applyAction _ _ _ _ = NotAppliedAction

getContinuation : Input â†’ Case â†’ Maybe Contract
getContinuation (NormalInput _) (mkCase _ continuation) = just continuation


data ApplyResult : Set where
  Applied : ApplyWarning â†’ State â†’ Contract â†’ ApplyResult
  ApplyNoMatchError : ApplyResult
  ApplyHashMismatch : ApplyResult


applyCases : Environment â†’ State â†’ Input â†’ List Case â†’ ApplyResult
applyCases env state input (headCase âˆ· tailCase)
  with applyAction env state (getInputContent input) (getAction headCase) | getContinuation input headCase
... | NotAppliedAction               | _                 = applyCases env state input tailCase
... | AppliedAction warning newState | just continuation = Applied warning newState continuation
... | _                              | nothing           = ApplyHashMismatch
applyCases _ _ _ [] = ApplyNoMatchError


applyInput : Environment â†’ State â†’ Input â†’ Contract â†’ ApplyResult
applyInput env state input (When cases _ _) = applyCases env state input cases
applyInput _ _ _ _ = ApplyNoMatchError


convertReduceWarnings : List ReduceWarning -> List TransactionWarning
convertReduceWarnings =
  foldr convertReduceWarning []
    where
      convertReduceWarning : ReduceWarning â†’ List TransactionWarning â†’ List TransactionWarning
      convertReduceWarning ReduceNoWarning acc = acc
      convertReduceWarning (ReduceNonPositivePay accId payee tok amount) acc = TransactionNonPositivePay accId payee tok amount âˆ· acc
      convertReduceWarning (ReducePartialPay accId payee tok paid expected) acc = TransactionPartialPay accId payee tok paid expected âˆ· acc
      convertReduceWarning (ReduceShadowing valId oldVal newVal) acc = TransactionShadowing valId oldVal newVal âˆ· acc
      convertReduceWarning ReduceAssertionFailed acc = TransactionAssertionFailed âˆ· acc


data ApplyAllResult : Set where
  ApplyAllSuccess : Bool â†’ List TransactionWarning â†’ List Payment â†’ State â†’ Contract â†’ ApplyAllResult
  ApplyAllNoMatchError : ApplyAllResult
  ApplyAllAmbiguousTimeIntervalError : ApplyAllResult
  ApplyAllHashMismatch : ApplyAllResult


applyAllInputs : Environment â†’ State â†’ Contract â†’ List Input â†’ ApplyAllResult
applyAllInputs env state contract inputs =
  applyAllLoop false env state contract inputs [] []
    where
      convertApplyWarning : ApplyWarning -> List TransactionWarning
      convertApplyWarning (ApplyNoWarning) = []
      convertApplyWarning (ApplyNonPositiveDeposit party accId tok amount) =
        TransactionNonPositiveDeposit party accId tok amount âˆ· []
      applyAllLoop : Bool â†’ Environment â†’ State â†’ Contract â†’ List Input â†’ List TransactionWarning â†’ List Payment â†’ ApplyAllResult
      applyAllLoop contractChanged env state contract inputs warnings payments
        with reduceContractUntilQuiescent env state contract | inputs
      ... | RRAmbiguousTimeIntervalError | _ = ApplyAllAmbiguousTimeIntervalError
      ... | ContractQuiescent reduced reduceWarns pays curState cont | [] =
              ApplyAllSuccess (contractChanged âˆ¨ reduced) (warnings ++ convertReduceWarnings reduceWarns) (payments ++ pays) curState cont
      ... | ContractQuiescent reduced reduceWarns pays curState cont | input  âˆ· rest
              with applyInput env curState input cont
      ...       | Applied applyWarn newState cont =
                    applyAllLoop true env newState cont rest (warnings ++ convertReduceWarnings reduceWarns ++ convertApplyWarning applyWarn) (payments ++ pays)
      ...       | ApplyNoMatchError = ApplyAllNoMatchError
      ...       | ApplyHashMismatch = ApplyAllHashMismatch


isClose : Contract â†’ Bool
isClose Close = true
isClose _     = false


notClose : Contract â†’ Bool
notClose Close = false
notClose _     = true


computeTransaction : TransactionInput â†’ State â†’ Contract â†’ TransactionOutput
computeTransaction (mkTransactionInput txInterval txInput) state contract
  with fixInterval txInterval state
... | mkIntervalError error = mkError (TEIntervalError error)
... | IntervalTrimmed env fixState with applyAllInputs env fixState contract txInput
... | ApplyAllNoMatchError = mkError TEApplyNoMatchError
... | ApplyAllAmbiguousTimeIntervalError = mkError TEAmbiguousTimeIntervalError
... | ApplyAllHashMismatch = mkError TEHashMismatch
... | ApplyAllSuccess reduced warnings payments newState cont =
        if not reduced âˆ§ (notClose contract âˆ¨ null (State.accounts state))
          then mkError TEUselessTransaction
          else mkTransactionOutput warnings payments newState cont


playTraceAux : TransactionOutput â†’ List TransactionInput â†’ TransactionOutput
playTraceAux res [] = res
playTraceAux (mkTransactionOutput warnings payments state contract) (h âˆ· t)
  with computeTransaction h state contract
... | mkTransactionOutput warnings' payments' state' contract' =
       playTraceAux (mkTransactionOutput (warnings ++ warnings') (payments ++ payments') state' contract') t
... | mkError error = mkError error
playTraceAux (mkError error) _ = mkError error

playTrace : PosixTime â†’ Contract â†’ List TransactionInput â†’ TransactionOutput
playTrace minTime c = playTraceAux (mkTransactionOutput [] [] (emptyState minTime) c)

